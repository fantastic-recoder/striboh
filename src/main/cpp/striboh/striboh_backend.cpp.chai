global theRunNo=0;

def stribohIdlServantInit() {
   stribohIdlSetServantRuns(2);// set tree visitor runs
}

global theIncludeGuard = "K_ECHO_SERVANT_GUARD_HPP";
global theHeaderCode = "";

def stribohIdlServantBeginRun(pRun){
   ++theRunNo;
   if(theRunNo==1) {
      theHeaderCode += "#ifndef ${theIncludeGuard}\n";
      theHeaderCode += "#define ${theIncludeGuard}\n";
      theHeaderCode += "#include <string>\n";
      theHeaderCode += "#include <vector>\n";
      theHeaderCode += "#include <striboh/stribohBaseEMessageType.hpp>\n";
      theHeaderCode += "#include <striboh/stribohBaseParameterList.hpp>\n";
      theHeaderCode += "#include <striboh/stribohBaseMethod.hpp>\n";
      theHeaderCode += "#include <striboh/stribohBaseMessage.hpp>\n";
      theHeaderCode += "#include <striboh/stribohBaseInterface.hpp>\n";
      theHeaderCode += "#include <striboh/stribohBaseServantBase.hpp>\n";
      theHeaderCode += "\n";
   }
}

def concatPrefix(pDepth) {
   var myPrefix="";
   for(var ii=0; ii<pDepth; ++ii){
      myPrefix=myPrefix+"\t";
   }
   return myPrefix;
}

def concatStringList(pList) {
   var myRetVal="";
   for(var ii=0; ii<pList.size(); ++ii){
      if(ii>0){
          myRetVal=myRetVal+",";
      }
      myRetVal=myRetVal+"\""+pList[ii]+"\"";
   }
   return myRetVal;
}

global theModuleDepth = 0;
global theModuleNames = [];
global theModuleCodeBegin = "";

def generateClientNamespace(pInterfaceName) {
    if(!theModuleNames.empty()) {
        addClientCode(theFilename, "namespace ");
    }
    for( var ii=0; ii<theModuleNames.size(); ++ii ) {
         if(ii>0) {
             addClientCode(theFilename, "::");
         }
         var myNamespace=theModuleNames[ii];
         addClientCode(theFilename, "${myNamespace}");
    }
    if(!theModuleNames.empty()) {
         addClientCode(theFilename, " {\n");
    }
}

def generateServantNamespace(pInterfaceName) {
    if(!theModuleNames.empty()) {
        addServantCode(theFilename, "namespace ");
    }
    for( var ii=0; ii<theModuleNames.size(); ++ii ) {
         if(ii>0) {
             addServantCode(theFilename, "::");
         }
         var myNamespace=theModuleNames[ii];
         addServantCode(theFilename, "${myNamespace}");
    }
    if(!theModuleNames.empty()) {
         addServantCode(theFilename, " {\n");
    }
}

def generateNamespaceClosing() {
    if(!theModuleNames.empty()) {
        addClientCode(theFilename, "} // end namespace \n");
    }
}


def stribohIdlServantBeginModule(pModuleName) {
   var myPrefix=concatPrefix(theModuleDepth);
   theModuleNames.push_back(pModuleName);
   ++theModuleDepth;
}

def stribohIdlServantEndModule(pModuleName) {
   --theModuleDepth;
   theModuleNames.pop_back();
}

global theMethodNo;
global theFilename="unknown";

def stribohIdlServantBeginInterface(pInterfaceName) {
   theMethodNo = 0;
   var myPrefix=concatPrefix(theModuleDepth);
   if(theRunNo==1) {
       theFilename = "${pInterfaceName}.hpp";
       addServantCode(theFilename, theHeaderCode);
       generateServantNamespace(pInterfaceName);
       addServantCode(theFilename, "${myPrefix} class ${pInterfaceName} : public striboh::base::ServantBase {\n");
       addServantCode(theFilename, "${myPrefix} public:\n");
   }
   if(theRunNo==2) {
       var myModuleList = concatStringList(theModuleNames);
       addServantCode(theFilename, "\n");
       addServantCode(theFilename, "${myPrefix}\t virtual const striboh::base::Interface& getInterface() const override { return mInterface; }\n\n");
       addServantCode(theFilename, "${myPrefix} private:\n");
       addServantCode(theFilename, "${myPrefix}\t striboh::base::Interface mInterface{\n");
       addServantCode(theFilename, "${myPrefix}\t\t *this,\n");
       print("Module list:${myModuleList}");
       addServantCode(theFilename, "${myPrefix}\t\t {${myModuleList}},\n");
       addServantCode(theFilename, "${myPrefix}\t\t striboh::base::InterfaceName{\"${pInterfaceName}\"},\n");
       addServantCode(theFilename, "${myPrefix}\t\t {\n");
   }
   ++theModuleDepth;
}

def stribohIdlServantEndInterface(pInterfaceName) {
   --theModuleDepth;
   var myPrefix=concatPrefix(theModuleDepth);
   if(theRunNo==2) {
       addServantCode(theFilename, "${myPrefix}\t\t }\n");
       addServantCode(theFilename, "${myPrefix}\t };\n");
       addServantCode(theFilename, "\n${myPrefix}};\n");
       addServantCode(theFilename, "\n} // end namespace\n");
       addServantCode(theFilename, "#endif // ${theIncludeGuard}");

   }
}

def stribohTypeToCpp(pStribohType) {
    if(pStribohType == "STRING") {
        return "std::string";
    }
    if(pStribohType == "INT") {
        return "int64_t";
    }
    throw (runtime_error("Unknown striboh type ${pStribohType}"));
}

def stribohTypeToTypeName(pStribohType) {
    if(pStribohType == "STRING") {
        return "striboh::base::ETypes::K_STRING";
    }
    if(pStribohType == "INT") {
        return "striboh::base::ETypes::K_INT";
    }
    throw (runtime_error("Unknown striboh type ${pStribohType}"));
}

global theParNo = -1;
global theLambdaParameterList;

def stribohIdlServantBeginMethod(pMethodName,pReturnType) {
   theParNo = 0;
   theLambdaParameterList = "";
   if(theRunNo==1) {
       var myPrefix=concatPrefix(theModuleDepth);
       var myCppType=stribohTypeToCpp(pReturnType);
       addServantCode(theFilename, "\n${myPrefix} virtual ${myCppType} ${pMethodName}(");
   }
   if(theRunNo==2) {
       var myPrefix=concatPrefix(theModuleDepth+1);
       if(theMethodNo>0) {
           addServantCode(theFilename, "${myPrefix}\t ,\n");
       }
       addServantCode(theFilename, "${myPrefix}\t striboh::base::Method{\"${pMethodName}\",\n");
       addServantCode(theFilename, "${myPrefix}\t\t striboh::base::ParameterDescriptionList{\n");
       addServantCode(theFilename, "${myPrefix}\t\t\t {\n");
   }
}

def stribohIdlServantEndMethod(pMethodName,pMethodType) {
   if(theRunNo==1) {
      addServantCode(theFilename, " ) = 0;\n");
   }
   if(theRunNo==2) {
      var myPrefix=concatPrefix(theModuleDepth+1);
      addServantCode(theFilename, "${myPrefix}\t\t\t }\n");
      addServantCode(theFilename, "${myPrefix}\t\t },\n");
      addServantCode(theFilename, "${myPrefix}\t\t [this](const striboh::base::Message &pIncoming,striboh::base::Context pCtx) {\n");
      addServantCode(theFilename, "${myPrefix}\t\t\t auto myRetVal=${pMethodName}(\n");
      addServantCode(theFilename, theLambdaParameterList);
      addServantCode(theFilename, "${myPrefix}\t\t\t );\n");
      addServantCode(theFilename, "${myPrefix}\t\t\t return striboh::base::Message(striboh::base::Value{myRetVal},getLog());\n");
      addServantCode(theFilename, "${myPrefix}\t\t },\n");
      addServantCode(theFilename, "${myPrefix}\t\t getLog()\n");
      addServantCode(theFilename, "${myPrefix}\t }\n");
   }
   theMethodNo += 1;
}

def stribohIdlServantBeginParameter(pParameterName,pType) {
   var myCppType=stribohTypeToCpp(pType);
   if(theRunNo==1) {
       if(theParNo>0) {
          addServantCode(theFilename, ", ");
       }
       addServantCode(theFilename, "const ${myCppType} & ${pParameterName}");
   }
   if(theRunNo==2) {
       var myPrefix = concatPrefix(theModuleDepth+1);
       var myParameterTypeName = stribohTypeToTypeName(pType);
       if(theParNo>0) {
          theLambdaParameterList += "${myPrefix}\t\t\t ,\n";
          addServantCode(theFilename, "${myPrefix}\t\t\t\t\t ,\n");
       }
      theLambdaParameterList += "${myPrefix}\t\t\t pIncoming.getParameters()[${theParNo}].getValue().get<${myCppType}>()\n";
      addServantCode(theFilename, "${myPrefix}\t\t\t\t\t striboh::base::ParameterDescription{striboh::base::EDirection::K_IN, ${myParameterTypeName}, \"${pParameterName}\"}\n");
   }
   theParNo = theParNo+1;
}

def stribohIdlClientInit() {
   theRunNo=0;
   stribohIdlSetClientRuns(2);// set tree visitor runs
}

def stribohIdlClientBeginRun(pRunNo) {
    ++theRunNo;
}

def stribohIdlClientBeginModule(pModuleName) {
   theModuleNames.push_back(pModuleName);
}

def stribohIdlClientEndModule(pModuleName) {
   theModuleNames.pop_back();
}

global theMethodMessageCode = [];
global theMethodCounter = 0;

def generateModulePath(pModuleNames) {
    var myModulePath = "";
    for( var ii=0; ii<theModuleNames.size(); ++ii ) {
        if( ii == 0 ) {
            myModulePath += theModuleNames[ii];
        } else {
            myModulePath += "/";
            myModulePath += theModuleNames[ii];
        }
    }
    if(!theModuleNames.empty()) {
        myModulePath += "/";
    }
    return myModulePath;
}

def stribohIdlClientBeginInterface(pInterfaceName) {
    theIncludeGuard = "${pInterfaceName}_CLIENT_GUARD_HPP";
    var myPrefix=concatPrefix(theModuleDepth);
    theMethodCounter = 0;
    if(theRunNo==1) {
        theMethodMessageCode.clear();
        theFilename = "${pInterfaceName}Clt.hpp"
    }
   if(theRunNo==2) {
        addClientCode(theFilename,"#ifndef ${theIncludeGuard}\n");
        addClientCode(theFilename,"#define ${theIncludeGuard}\n");
        addClientCode(theFilename,"#include <string>\n");
        addClientCode(theFilename,"#include <vector>\n");
        addClientCode(theFilename,"#include <striboh/stribohBaseEMessageType.hpp>\n");
        addClientCode(theFilename,"#include <striboh/stribohBaseParameterList.hpp>\n");
        addClientCode(theFilename,"#include <striboh/stribohBaseMethod.hpp>\n");
        addClientCode(theFilename,"#include <striboh/stribohBaseMessage.hpp>\n");
        addClientCode(theFilename,"#include <striboh/stribohBaseInterface.hpp>\n");
        addClientCode(theFilename,"#include <striboh/stribohBaseProxyBase.hpp>\n");
        addClientCode(theFilename,"#include <striboh/stribohBaseBroker.hpp>\n");
        addClientCode(theFilename,"\n");
        generateClientNamespace(pInterfaceName);
        var myModulePath = generateModulePath(theModuleNames);
        addClientCode(theFilename,"${myPrefix}\t class ${pInterfaceName}Proxy : public striboh::base::ProxyBase {\n");// starting class ${pInterfaceName}Proxy\n");
        addClientCode(theFilename,"${myPrefix}\t public:\n");
        addClientCode(theFilename,"${myPrefix}\t\t ${pInterfaceName}Proxy(  std::string_view pHost, short pPort, striboh::base::LogIface& pLogIface )\n");
        addClientCode(theFilename,"${myPrefix}\t\t\t : striboh::base::ProxyBase(pHost, pPort, \"/${myModulePath}${pInterfaceName}\", pLogIface ) {}\n");
   }
   ++theModuleDepth;
}

def stribohIdlClientEndInterface(pInterfaceName) {
   var myPrefix=concatPrefix(theModuleDepth);
   if(theRunNo==2) {
        addClientCode(theFilename,"${myPrefix} };// end class ${pInterfaceName}\n");
        generateNamespaceClosing();
        addClientCode(theFilename,"#endif // ${theIncludeGuard}\n");
   }
   --theModuleDepth;
}

def stribohIdlClientBeginMethod(pMethodName,pReturnType) {
   var myMethodType = stribohTypeToCpp(pReturnType);
   var myPrefix=concatPrefix(theModuleDepth);
   if(theRunNo==1) {
       theParNo = 0;
       theMethodMessageCode.push_back("${myPrefix} striboh::base::Message ${pMethodName}Msg(\"${pMethodName}\", {\n");
   }
   if(theRunNo==2) {
       theParNo = 0;
       addClientCode(theFilename, "${myPrefix}\t striboh::base::RetValProxy<${myMethodType}> ${pMethodName}(");
   }
}

def stribohIdlClientEndMethod(pMethodName,pReturnType) {
   var myPrefix=concatPrefix(theModuleDepth);
   if(theRunNo==1) {
       theMethodMessageCode[theMethodCounter] += "\n${myPrefix}\t\t } , getLog() ); // end method message"
   }
   if(theRunNo==2) {
       var myCppType=stribohTypeToCpp(pReturnType);
       addClientCode(theFilename, "){\n");
       addClientCode(theFilename, "${myPrefix}\t ${theMethodMessageCode[theMethodCounter]}\n");
       addClientCode(theFilename, "${myPrefix}\t\t return invoke(std::forward<striboh::base::Message>(${pMethodName}Msg), ${myCppType}{} ); // send request\n");
       addClientCode(theFilename, "${myPrefix}\t } // end method\n");
   }
   ++theMethodCounter;
}

def stribohIdlClientBeginParameter(pParameterName, pParameterType) {
   var myParameterType = stribohTypeToCpp(pParameterType);
   var myPrefix=concatPrefix(theModuleDepth);
   if(theRunNo==1) {
       if(theParNo==0) {
          theMethodMessageCode[theMethodCounter] += "${myPrefix}\t\t\t{ \"${pParameterName}\", ${pParameterName} }";
       } else {
          theMethodMessageCode[theMethodCounter] += ",\n${myPrefix}\t\t\t{ \"${pParameterName}\", ${pParameterName} }";
       }
       ++theParNo;
   }
   if(theRunNo==2) {
       if(theParNo>0) {
           addClientCode(theFilename, ", ");
       }
       addClientCode(theFilename, "${myParameterType} ${pParameterName}");
       ++theParNo;
   }
}
